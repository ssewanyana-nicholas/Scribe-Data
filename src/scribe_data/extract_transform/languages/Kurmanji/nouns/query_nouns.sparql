# All Kurmanji (Q36163) nouns and their gender.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") as ?lexemeID)
  ?noun
  ?gender

WHERE {
  # Nouns and pronouns.
  VALUES ?nounTypes { wd:Q1084 wd:Q147276 }
  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q36163 ;
    wikibase:lexicalCategory ?nounType ;
    wikibase:lemma ?noun .
  FILTER(?nounType = ?nounTypes)

  # Optional selection of genders.
  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS { ?lexeme wdt:P31 wd:Q202444 . }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
